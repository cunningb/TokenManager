import org.apache.tools.ant.filters.ReplaceTokens
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

group 'me.realized'
version '3.1.2'

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

project.tasks.assemble.dependsOn project.tasks.shadowJar
jar.enabled = false

compileJava.options.encoding = 'UTF-8'

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [VERSION: project.version])
    }
}

artifacts {
    archives jar
}

clean.doFirst {
    delete "$rootDir/out/"
}

tasks.withType(ShadowJar) {
    destinationDir = file("$rootDir/out/")
}

tasks.withType(Jar) {
    destinationDir = file("$rootDir/out/")
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        name 'Spigot'
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
    }

    maven {
        name 'Vault'
        url 'http://nexus.hc.to/content/repositories/pub_releases'
    }

    maven {
        name 'MVdW Public Repositories'
        url 'http://repo.mvdw-software.be/content/groups/public/'
    }

    maven {
        name 'PlaceholderAPI'
        url 'http://repo.extendedclip.com/content/repositories/placeholderapi/'
    }

    maven {
        name 'inventive-repo'
        url 'https://repo.inventivetalent.org/content/groups/public/'
    }

    maven {
        name 'bstats-repo'
        url 'http://repo.bstats.org/content/repositories/releases/'
    }
}

dependencies {
    compile 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
    compile 'org.spigotmc:spigot:1.12.2-R0.1-SNAPSHOT'
    compile 'net.milkbowl.vault:VaultAPI:1.6'
    compile 'me.clip:placeholderapi:2.8.7'
    compile 'be.maximvdw:MVdWPlaceholderAPI:2.5.1-SNAPSHOT'
    compile 'org.projectlombok:lombok:1.16.20'

    compile 'redis.clients:jedis:2.9.0'
    compile 'org.apache.commons:commons-pool2:2.4.2'
    compile 'com.zaxxer:HikariCP:2.4.7'
    compile 'org.inventivetalent.spiget-update:bukkit:1.4.0'
    compile 'org.bstats:bstats-bukkit:1.2'
}

shadowJar {
    baseName = 'TokenManager'
    classifier = null

    dependencies {
        include(dependency('com.zaxxer:.*'))
        include(dependency('org.slf4j:.*'))
        include(dependency('org.inventivetalent.spiget-update:.*'))
        include(dependency('org.bstats:.*'))
    }

    def group = project.group.toString() + "." + project.name.toLowerCase() + ".shaded."

    relocate 'org.inventivetalent.update.spiget', group + 'update.spiget'
    relocate 'org.bstats', group + 'bstats'
}